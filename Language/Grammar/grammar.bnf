//Jack Herakovich https://github.com/JackHerakovich/Programming-Languages/tree/main/Language
program ::=statementList;

statementList ::= statement
                | statement statementList;

statement ::= function
            | variableInitialization
            | assignment
            | forLoop
            | whileLoop
            | infiniteLoop
            | expression
            | ifStatement
            | arrayInitialization
            | arrayListInitialization
            | functionCall;

variableInitialization::="¡" IDENTIFIER "=" expression;

expression::= primary binaryOperator primary
            | primary unaryOperator
            | unaryOperator primary
            | primary binaryOperator expression
            | primary;

forLoop::= "≈" "(" variableInitialization ";" comparison ";" expression ")" block;

whileLoop::= "◊" "(" comparison ")" block;

infiniteLoop::= "∞" block;

ifStatement ::= "›" "(" comparison ")" block
              | "›" "(" comparison ")" block "»" ifStatement
              | "›" "(" comparison ")" block elseStatement
              | "›" "(" comparison ")" block "»" ifStatement elseStatement;

elseStatement::= "‹" block;

function::= "∏" IDENTIFIER "(" parameter? ")" block;

parameter::= VARIABLE
           | VARIABLE "," parameter
           | function;

block::="¿" statementList "?";

comparison::=primary comparator primary
            | boolean;

comparator::= "=="
            | ">"
            | "<"
            | "≥"
            | "≤";

unaryOperator::= "++"
               | "-"
               | "--";


binaryOperator::=  "+"
           | "-"
           | "*"
           | "%"
           | "/";

arrayInitialization::= "Å" "(" expression ")" IDENTIFIER;

arrayListInitialization::= "Ålist" "(" ")" IDENTIFIER;

functionCall::= IDENTIFIER "(" functionInput? ")";

functionInput::= primary
               | primary "," functionInput;

assignment::= VARIABLE "=" expression
            | VARIABLE unaryOperator;

boolean::= "true"
         | "false";

primary ::= VARIABLE
          | INTEGER
          | STRING
          | DOUBLE
          | CHARACTER
          | boolean;

printStatement::= "print" "("expression")"
